# Author: mosra <mosra@centrum.cz>
pkgname=emscripten-corrade
pkgver=dev
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (Emscripten)"
arch=('any')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'emscripten' 'ninja')
options=(!strip !buildflags)

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-emscripten" ] ; then
        mkdir "$_rootdir/build-emscripten"
        cd "$_rootdir/build-emscripten"

        cmake .. \
            -DCMAKE_TOOLCHAIN_FILE="$_rootdir/toolchains/generic/Emscripten.cmake" \
            -G Ninja
    fi

    cd "$_rootdir/build-emscripten"

    # Put tests into a separate target as they take ages to build. They're
    # built only inside check() so `makepkg --nocheck` will skip building them.
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/emscripten/system \
        -DCORRADE_BUILD_TESTS=ON \
        -DCORRADE_TESTSUITE_TEST_TARGET=build-tests
    ninja
}

check() {
    cd "$_rootdir/build-emscripten"

    ninja build-tests

    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-emscripten"
    DESTDIR="$pkgdir/" ninja install
}
