# Author: mosra <mosra@centrum.cz>
pkgname=corrade
pkgver=dev.gcc48
pkgrel=1
pkgdesc="C++11/C++14 multiplatform utility library (built with GCC 4.8)"
arch=('i686' 'x86_64')
url="https://magnum.graphics/corrade/"
license=('MIT')
makedepends=('cmake' 'ninja' 'gcc48')
options=(!strip !buildflags)
provides=('corrade-git')

_rootdir=$startdir/../../

build() {
    if [ ! -d "$_rootdir/build-gcc48" ] ; then
        mkdir "$_rootdir/build-gcc48"
        cd "$_rootdir/build-gcc48"

        cmake .. \
            -DCMAKE_CXX_COMPILER=g++-4.8 \
            -G Ninja
    fi

    cd "$_rootdir/build-gcc48"

    cmake .. \
        -DCMAKE_BUILD_TYPE=Debug \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCORRADE_BUILD_TESTS=ON
    ninja
}

check() {
    cd "$_rootdir/build-gcc48"
    CORRADE_TEST_COLOR=ON ctest --output-on-failure
}

package() {
    cd "$_rootdir/build-gcc48"
    DESTDIR="$pkgdir/" ninja install
}
